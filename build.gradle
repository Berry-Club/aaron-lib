import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
	id 'idea'
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version '[6.0,6.2)'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
	id 'org.jetbrains.kotlin.jvm' version '2.0.0'
	id "me.shedaniel.unified-publishing" version "0.1.+"
}

def ENV = System.getenv()
def isLocal = !ENV.containsKey("GITHUB_RUN_NUMBER")

def archiveName = "${mod_id.replace("_", "-")}-${minecraft_version}"

version = "${mod_version}-${isLocal ? "local.${Instant.now().epochSecond}" : "build.${ENV.GITHUB_RUN_NUMBER}"}"
group = project.mod_group_id
base.archivesName = archiveName

println("Building version: ${version}")

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
compileKotlin.compilerOptions.jvmTarget = JvmTarget.JVM_21

minecraft {
	mappings channel: mapping_channel, version: mapping_version
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		// REQUIRED for processResources to work in dev
		copyIdeResources = true

		client {
			workingDirectory project.file('runs/client')

			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
			property 'forge.enabledGameTestNamespaces', mod_id

			mods {
				"$mod_id" {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('runs/server')

			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
			property 'forge.enabledGameTestNamespaces', mod_id

			mods {
				"$mod_id" {
					source sourceSets.main
				}
			}
		}

		gameTestServer {
			workingDirectory project.file('runs/server')

			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
			property 'forge.enabledGameTestNamespaces', mod_id

			mods {
				"$mod_id" {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('runs/client')

			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')

			mods {
				"$mod_id" {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
	runtimeClasspath.extendsFrom localRuntime
}

repositories {
	mavenLocal()

	maven {
		name = 'Kotlin for Forge'
		url = 'https://thedarkcolour.github.io/KotlinForForge/'
	}

	// For EMI
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}

	// For Curios
	repositories {
		maven {
			name = "Illusive Soulworks maven"
			url = "https://maven.theillusivec4.top/"
		}
	}

	// For Modonomicon
	maven {
		url "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
		content {
			includeGroup "com.klikli_dev"
		}
	}

	maven {
		url "https://maven.latvian.dev/releases"
		content {
			includeGroup "dev.aaronhowser.mods"
		}
	}

	maven { url 'https://maven.blamejared.com' }

}

dependencies {
	minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"
	implementation "thedarkcolour:kotlinforforge-neoforge:${kff_version}"
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
		minecraft_version      : minecraft_version,
		minecraft_version_range: minecraft_version_range,
		forge_version          : forge_version,
		forge_version_range    : forge_version_range,
		mod_id                 : mod_id,
		mod_name               : mod_name,
		mod_license            : mod_license,
		mod_version            : mod_version,
		mod_authors            : mod_authors,
		mod_description        : mod_description,
		kff_version_range      : kff_version_range
]

processResources {
	inputs.properties replaceProperties
	replaceProperties.put 'project', project

	filesMatching(resourceTargets) {
		expand replaceProperties
	}
}

publishing {
	publications {
		mavenNeoForge(MavenPublication) {
			artifactId = archiveName
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_TOKEN
				}
			}
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

unifiedPublishing {
	project {
		displayName = "Aaron ${project.minecraft_version} ${project.version}"
		gameVersions = [project.minecraft_version]
		gameLoaders = ["forge"]

		changelog = "https://github.com/Berry-Club/aaron-lib/blob/master/CHANGELOG.md#" + project.mod_version.toString().replace(".", "")

		mainPublication jar

		relations {
			depends {
				curseforge = modrinth = "kotlin-for-forge"
			}
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
			}
		}

	}
}